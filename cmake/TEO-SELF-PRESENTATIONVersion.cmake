include(GitInfo) # YCM

# Define current version.
set(TEO-SELF-PRESENTATION_VERSION_SHORT ${TEO-SELF-PRESENTATION_VERSION})

# Retrieve current revision from Git working tree.
git_wt_info(SOURCE_DIR "${CMAKE_SOURCE_DIR}"
            PREFIX TEO-SELF-PRESENTATION)

if(DEFINED TEO-SELF-PRESENTATION_GIT_WT_HASH)
    if(TEO-SELF-PRESENTATION_GIT_WT_TAG_REVISION GREATER 0)
        set(TEO-SELF-PRESENTATION_VERSION_REVISION ${TEO-SELF-PRESENTATION_GIT_WT_TAG_REVISION})
        string(REPLACE "-" "" _date ${TEO-SELF-PRESENTATION_GIT_WT_AUTHOR_DATE})
        set(TEO-SELF-PRESENTATION_VERSION_SOURCE
            "${_date}.${TEO-SELF-PRESENTATION_GIT_WT_DATE_REVISION}+git${TEO-SELF-PRESENTATION_GIT_WT_HASH_SHORT}")
    endif()

    if(TEO-SELF-PRESENTATION_GIT_WT_DIRTY)
        set(TEO-SELF-PRESENTATION_VERSION_DIRTY "dirty")
    endif()
endif()

if(DEFINED TEO-SELF-PRESENTATION_VERSION_SOURCE)
    if(NOT "${TEO-SELF-PRESENTATION_GIT_WT_TAG}" STREQUAL "v${TEO-SELF-PRESENTATION_VERSION_SHORT}")
        set(TEO-SELF-PRESENTATION_VERSION
            "${TEO-SELF-PRESENTATION_VERSION_SHORT}+${TEO-SELF-PRESENTATION_VERSION_SOURCE}")
    else()
        set(TEO-SELF-PRESENTATION_VERSION
           "${TEO-SELF-PRESENTATION_VERSION_SHORT}+${TEO-SELF-PRESENTATION_VERSION_REVISION}-${TEO-SELF-PRESENTATION_VERSION_SOURCE}")
    endif()
elseif(NOT "${TEO-SELF-PRESENTATION_GIT_WT_TAG}" STREQUAL "v${TEO-SELF-PRESENTATION_VERSION_SHORT}")
    set(TEO-SELF-PRESENTATION_VERSION "${TEO-SELF-PRESENTATION_VERSION_SHORT}~dev")
else()
    set(TEO-SELF-PRESENTATION_VERSION "${TEO-SELF-PRESENTATION_VERSION_SHORT}")
endif()

if(DEFINED TEO-SELF-PRESENTATION_VERSION_DIRTY)
    set(TEO-SELF-PRESENTATION_VERSION "${TEO-SELF-PRESENTATION_VERSION}+${TEO-SELF-PRESENTATION_VERSION_DIRTY}")
endif()

# Print version.
message(STATUS "TEO-SELF-PRESENTATION version: ${TEO-SELF-PRESENTATION_VERSION_SHORT} (${TEO-SELF-PRESENTATION_VERSION})")
